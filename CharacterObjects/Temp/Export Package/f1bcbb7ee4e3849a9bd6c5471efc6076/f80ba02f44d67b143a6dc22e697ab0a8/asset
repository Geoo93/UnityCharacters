Shader "TMG/Glass_NoCull" {
Properties {
	_Color ("Main Color", Color) = (1,1,1,1)
	_SpecColor ("Specular Color", Color) = (0.5,0.5,0.5,1)
	_Shininess ("Shininess", Range (0.01, 1)) = 0.078125
	_ReflectColor ("Reflection Color", Color) = (1,1,1,0.5)
	_MainTex ("Base (RGB) RefStrGloss (A)", 2D) = "white" {}
	_Cube ("Reflection Cubemap", Cube) = "" { TexGen CubeReflect }
	_BumpMap ("Normalmap", 2D) = "bump" {}
	_BumpStrength ("Bump Strength", Range(0.0, 1)) = 0.5
	_DirtTex ("Dirt RGB", 2D) = "white" {}
	_DirtStrength ("Dirt Strength", Range(0.0, 1)) = 0.0
}

SubShader {
	Tags { "RenderType"="Opaque" }
	LOD 400
	Cull Off
	
CGPROGRAM
#pragma surface surf BlinnPhong alpha
#pragma target 3.0

sampler2D _MainTex;
sampler2D _BumpMap;
samplerCUBE _Cube;

fixed4 _Color;
fixed4 _ReflectColor;
half _Shininess;
half _BumpStrength;

sampler2D _DirtTex;
half _DirtStrength;

struct Input {
	float2 uv_MainTex;
	float2 uv_BumpMap;
	float3 worldRefl;
	INTERNAL_DATA
};

void surf (Input IN, inout SurfaceOutput o) {
	fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
	fixed4 c = tex * _Color;
	fixed4 dirt = tex2D(_DirtTex, IN.uv_MainTex);
	o.Albedo = lerp(c.rgb, dirt.rgb, _DirtStrength);
	
	o.Gloss = tex.a;
	o.Specular = _Shininess;
	
	float3 baseNormal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));
	o.Normal.xyz = lerp(fixed3(0,0,1), baseNormal, _BumpStrength);
	
	float3 worldRefl = WorldReflectionVector (IN, o.Normal);
	fixed4 reflcol = texCUBE (_Cube, worldRefl);
	//reflcol *= tex.a;
	o.Emission = reflcol.rgb * _ReflectColor.rgb;
	o.Alpha = reflcol.a * _ReflectColor.a * _Color.a;
}
ENDCG
}

FallBack "Reflective/Bumped Diffuse"
}